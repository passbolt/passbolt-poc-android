// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext.kotlin_version = "1.4.20"

  ext.buildConfig = [
      'minSdk': 30,
      'compileSdk': 30,
      'targetSdk': 30
  ]

  ext.versions = [
      'kotlin': '1.4.20',
      'agp': '4.2.0-alpha16',
      'androidx': [
          'appcompat': '1.3.0-alpha02',
          'core': '1.5.0-alpha05',
          'constraintlayout': '2.1.0-alpha1',
          'navigationfragment': '2.3.1',
          'navigationui': '2.3.1',
          'recyclerview': '1.2.0-alpha06'
      ],
      'material': '1.3.0-alpha03',
      'ktlint': '0.39.0'
  ]

  ext.deps = [
      androidx: [
          appcompat: "androidx.appcompat:appcompat:${versions.androidx.appcompat}",
          core: "androidx.core:core-ktx:${versions.androidx.core}",
          constraintlayout: "androidx.constraintlayout:constraintlayout:${versions.androidx.constraintlayout}",
          navigationfragment: "androidx.navigation:navigation-fragment-ktx:${versions.androidx.navigationfragment}",
          navigationui: "androidx.navigation:navigation-ui-ktx:${versions.androidx.navigationui}",
          recyclerview: "androidx.recyclerview:recyclerview:${versions.androidx.recyclerview}"
      ],
      material: "com.google.android.material:material:${versions.material}",
  ]

  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${versions.agp}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

plugins {
  id "com.diffplug.spotless" version "5.8.2"
  id 'com.github.ben-manes.versions' version "0.36.0"
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }
}

subprojects {
  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      target '**/*.kt'
      targetExclude("$buildDir/**/*.kt")
      targetExclude('bin/**/*.kt')
      ktlint(versions.ktlint).userData(['indent_size': '2', 'continuation_indent_size': '4'])
    }
  }
}