// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

  ext.buildConfig = [
      'minSdk': 30,
      'compileSdk': 30,
      'targetSdk': 30
  ]

  ext.versions = [
      'kotlin': [
          'plugin': '1.4.21',
          'coroutines': '1.4.2'
      ],
      'agp': '4.2.0-beta03',
      'androidx': [
          'appcompat': '1.3.0-beta01',
          'core': '1.5.0-beta01',
          'constraintlayout': '2.1.0-alpha2',
          'navigationfragment': '2.3.2',
          'navigationui': '2.3.2',
          'recyclerview': '1.2.0-beta01',
          'biometric': '1.2.0-alpha01',
          'securitycrypto': '1.1.0-alpha03',
          camera: [
              'camera2': '1.0.0-rc01',
              'lifecycle': '1.0.0-rc01',
              'view': '1.0.0-alpha20'
          ]
      ],
      'material': '1.3.0-beta01',
      'barcodescanner': '16.1.0',
      'ktlint': '0.39.0',
      'owasp_dependency_check': '6.0.5',
      'test': [
          'junit': '4.13.1'
      ]
  ]

  ext.deps = [
      kotlin: [
          coroutines: "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.kotlin.coroutines}"
      ],
      androidx: [
          appcompat: "androidx.appcompat:appcompat:${versions.androidx.appcompat}",
          core: "androidx.core:core-ktx:${versions.androidx.core}",
          constraintlayout: "androidx.constraintlayout:constraintlayout:${versions.androidx.constraintlayout}",
          navigationfragment: "androidx.navigation:navigation-fragment-ktx:${versions.androidx.navigationfragment}",
          navigationui: "androidx.navigation:navigation-ui-ktx:${versions.androidx.navigationui}",
          recyclerview: "androidx.recyclerview:recyclerview:${versions.androidx.recyclerview}",
          biometric: "androidx.biometric:biometric-ktx:${versions.androidx.biometric}",
          securitycrypto: "androidx.security:security-crypto:${versions.androidx.securitycrypto}",
          camera: [
              camera2: "androidx.camera:camera-camera2:${versions.androidx.camera.camera2}",
              lifecycle: "androidx.camera:camera-lifecycle:${versions.androidx.camera.lifecycle}",
              view: "androidx.camera:camera-view:${versions.androidx.camera.view}"
          ]
      ],
      material: "com.google.android.material:material:${versions.material}",
      barcodescanner: "com.google.mlkit:barcode-scanning:${versions.barcodescanner}",
      test: [
          junit: "junit:junit:${versions.test.junit}"
      ],
  ]

  repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "com.android.tools.build:gradle:${versions.agp}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin.plugin}"
    classpath "org.owasp:dependency-check-gradle:${versions.owasp_dependency_check}"
    // NOTE: Do not place your application dependencies here; they belong
    // in the individual module build.gradle files
  }
}

plugins {
  id "com.diffplug.spotless" version "5.9.0"
  id "io.gitlab.arturbosch.detekt" version "1.15.0"
  id 'com.github.ben-manes.versions' version "0.36.0"
}

allprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
    flatDir {
      dirs 'libs'
    }
  }
}

subprojects {
  apply plugin: 'org.owasp.dependencycheck'

  dependencyCheck {
    formats = ['HTML', 'JUNIT']
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      target '**/*.kt'
      targetExclude("$buildDir/**/*.kt")
      targetExclude('bin/**/*.kt')
      ktlint(versions.ktlint).userData(['indent_size': '2', 'continuation_indent_size': '4'])
    }
  }

  detekt {
    toolVersion = "1.15.0-RC2"
    config = files("config/detekt/detekt.yml")
    buildUponDefaultConfig = true
  }
}